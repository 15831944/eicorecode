; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "FieldTech Manager"
!define PRODUCT_VERSION "x.x.x.x"
!define PRODUCT_PUBLISHER "Environmental Intellect"
!define PRODUCT_WEB_SITE "http://www.env-int.com"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKCU"
RequestExecutionLevel user

; MUI 1.67 compatible ------
!include "MUI2.nsh"
!include "LogicLib.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "D:\eicore\EnvInt.Win32\EnvInt.Win32.FieldTech\EnvInt.Win32.FieldTech.Toolbox\cube.ico"
!define MUI_UNICON "D:\eicore\EnvInt.Win32\EnvInt.Win32.FieldTech\EnvInt.Win32.FieldTech.Toolbox\cube.ico"
!define MUI_WELCOMEFINISHPAGE_BITMAP "D:\eicore\EnvInt.Win32\EnvInt.Win32.FieldTech\EnvInt.Win32.FieldTech.Toolbox\Resources\EiLogoTall.bmp"
!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_TEXT "Launch FieldTech Manager"
!define MUI_FINISHPAGE_RUN_FUNCTION "LaunchLink"

;setup script directory and input file(s)
!define baseFileDir "D:\eicore\EnvInt.Win32\EnvInt.Win32.FieldTech\EnvInt.Win32.FieldTech.Manager\obj\Release\"
!define scriptDir "D:\eicore\EnvInt.Win32\EnvInt.Win32.FieldTech\EnvInt.Win32.FieldTech.Toolbox.Install\"
!define installSubFolder "\Environmental Intellect\FieldTech Manager"
!define installerOutFile "FieldTechManager_Setup_${PRODUCT_VERSION}.exe"
!define defaultInstallDir "$LOCALAPPDATA\FieldTech_Manager"
!define linkName "FieldTechManager.lnk"
!define installFile "EnvInt.Win32.FieldTech.Manager.exe"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page

Page instFiles

Function LaunchLink
  ExecShell "" "$INSTDIR\${linkName}"
FunctionEnd

Function .onInit

IfFileExists $WINDIR\SYSWOW64\*.* Is64bit Is32bit
Is32bit:
    SetRegView 32
    GOTO End32Bitvs64BitCheck

Is64bit:
    SetRegView 64

End32Bitvs64BitCheck:

  ReadRegStr $R0 HKCU \
  "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" \
  "UninstallString"
  StrCmp $R0 "" done

  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION \
  "${PRODUCT_NAME} is already installed. $\n$\nClick `OK` to remove the \
  previous version or `Cancel` to cancel this upgrade." \
  IDOK uninst
  Abort

;Run the uninstaller
uninst:
  ClearErrors
  ExecWait '$R0 _?=$INSTDIR' ;Do not copy the uninstaller to a temp file

  IfErrors no_remove_uninstaller done
    ;You can either use Delete /REBOOTOK in the uninstaller or add some code
    ;here to remove the uninstaller. Use a registry key to check
    ;whether the user has chosen to uninstall. If you are using an uninstaller
    ;components page, make sure all sections are uninstalled.
  no_remove_uninstaller:

done:

FunctionEnd

!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile ${installerOutFile}
InstallDir ${defaultInstallDir}
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  SetShellVarContext current
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File /r "${baseFileDir}"
  CreateShortCut "$DESKTOP\${linkName}" "$INSTDIR\${installFile}"
  CreateShortCut "$INSTDIR\${linkName}" "$INSTDIR\${installFile}"
  WriteRegStr HKCU "Software\Classes\.eip" "" "FieldTechManager.Document"
  WriteRegStr HKCU "Software\Classes\FieldTechManager.Document" "" "FieldTechManager Document"
  WriteRegStr HKCU "Software\Classes\FieldTechManager.Document\DefaultIcon" "" "$INSTDIR\${installFile},0"
  WriteRegStr HKCU "Software\Classes\FieldTechManager.Document\shell\open\command" "" '"$INSTDIR\${installFile}" "%1"'
  SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateDirectory "$SMPROGRAMS\Environmental Intellect"
  CreateDirectory "$SMPROGRAMS${installSubFolder}"
  CreateShortCut "$SMPROGRAMS${installSubFolder}\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS${installSubFolder}\${linkName}" "$INSTDIR\${installFile}"
  CreateShortCut "$SMPROGRAMS${installSubFolder}\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  CreateDirectory "$INSTDIR\Resources"
  ;WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "${PRODUCT_NAME}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "QuietUninstallString" "$\"$INSTDIR\uninst.exe$\" /S"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\uninst.exe,0"
  
  ;AccessControl::GrantOnFile "$INSTDIR\${installFile}" "(BU)" "FullAccess"
  ;AccessControl::GrantOnFile "$INSTDIR" "(BU)" "FullAccess"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit

  IfFileExists $WINDIR\SYSWOW64\*.* Is64bit Is32bit
  Is32bit:
    SetRegView 32
    GOTO End32Bitvs64BitCheck

  Is64bit:
    SetRegView 64

  End32Bitvs64BitCheck:
  
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\DataSets\*"
  Delete "$INSTDIR\Report Templates\*"
  Delete "$INSTDIR\Resources\*"
  Delete "$INSTDIR\TempPE\*"
  Delete "$INSTDIR\zh-CHS\*"
  Delete "$INSTDIR\*"

  Delete "$SMPROGRAMS${installSubFolder}\Uninstall.lnk"
  Delete "$SMPROGRAMS${installSubFolder}\Website.lnk"
  Delete "$SMPROGRAMS${installSubFolder}\${linkName}"
  Delete "$DESKTOP\${linkName}"
  
  RMDir "$SMPROGRAMS${installSubFolder}"
  RMDir /r "$INSTDIR"
  RMDir ""
  
  DeleteRegKey HKCU "Software\Classes\.eip"
  DeleteRegKey HKCU "Software\Classes\FieldTechManager.Document"
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"

  SetAutoClose true
SectionEnd